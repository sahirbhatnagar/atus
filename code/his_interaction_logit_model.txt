##################################
# JAGS model to be used in jags_model.R for logistic regression (with autoregressive structure)
# Created by kevisco, May 7th, 2014
# Updated May 16th, 2014
# hosted on Github repo 'sahirbhatnagar/atus'
# NOTE:
##################################

model {
    for (i in 1:N) {
        RESPONSE[i] ~ dbern(p[i])
        p[i] <- ilogit(alpha_diary[DIARYDAY[i]] + alpha_region[REGION[i]] + alpha_hispanic * HISPANIC[i] + alpha_sex * SEX[i] + alpha_race[RACE[i]] 
	      	 			    + beta_econ1 * ECON1[i] + beta_econ2 * ECON2[i] + gamma[MONTH[i],YEAR[i]]
					    + beta_i7 * HISPANIC[i] * ECON1[i] + beta_i8 * HISPANIC[i] * ECON2[i])


    }



    #first do the time trend for 1st year
      gammamean[1,1] <- 0.0
      gammaprec[1,1] <- phi * 0.001
      gammamean[2,1] <- 0.0
      gammaprec[2,1] <- phi * 0.001
    
          for (i in 3:12) {
              gammamean[i,1] <- 2 * gamma[i-1,1] - gamma[i-2,1]
              gammaprec[i,1] <- phi
          }

    # now for years 2 to 10
    for (j in 2:10){
    
      gammamean[1,j] <- 2 * gamma[12,j-1] - gamma[11,j-1]
      gammaprec[1,j] <- phi
      gammamean[2,j] <- 2 * gamma[1,j] - gamma[12,j-1]
      gammaprec[2,j] <- phi
    
          for (i in 3:12) {
              gammamean[i,j] <- 2 * gamma[i-1,j] - gamma[i-2,j]
              gammaprec[i,j] <- phi
          }
    }


    for (i in 1:12) {
        for (j in 1:10) {
            gamma[i,j] ~ dnorm(gammamean[i,j],gammaprec[i,j])
        }
    }


    beta_econ1 ~ dnorm(0.0,0.001)
    beta_econ2 ~ dnorm(0.0,0.001)

    phi ~ dgamma(0.001,0.001)

    for (i in 1:4) {
    	alpha_race[i] ~ dnorm(0.0,0.001)
    }
    alpha_race[5] <- 0.0

    alpha_sex ~ dnorm(0.0,0.001)

    alpha_hispanic ~ dnorm(0.0,0.001)
    beta_i7 ~ dnorm(0.0,0.001)
    beta_i8 ~ dnorm(0.0,0.001)


    for (i in 1:3) {
    	alpha_region[i] ~ dnorm(0.0,0.001)
    }
    alpha_region[4] <- 0.0

    for (i in 1:6) {
    	alpha_diary[i] ~ dnorm(0.0,0.001)	
    }
    alpha_diary[7] <- 0.0

}
